apply plugin: 'eclipse'
apply plugin: 'java'

buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    apply plugin: "maven-publish"
    apply plugin: "signing"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId = 'adzerk-decision-sdk'
            group = 'com.adzerk'
            version = '1.0.0-beta.15'

            pom {
                name = 'Adzerk Decision SDK'
                description = 'Adzerk Decision SDK'
                url = 'https://github.com/adzerk/adzerk-decision-sdk-java'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kevel'
                        name = 'Kevel'
                        email = 'engineering@kevel.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/adzerk/adzerk-decision-sdk-java.git'
                    developerConnection = 'scm:git:ssh://github.com/adzerk/adzerk-decision-sdk-java.git'
                    url = 'http://github.com/adzerk/adzerk-decision-sdk-java/'
                }
            }

            signing {
                sign publishing.publications.mavenJava
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/adzerk/adzerk-decision-sdk-java"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = "ossrh-staging-api"
            url = uri(version.endsWith("SNAPSHOT")
                ? "https://central.sonatype.com/repository/maven-snapshots/"
                : "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/")

            credentials {
                username = System.getenv("NEXUS_USERNAME") ?: findProperty("nexusUsername")
                password = System.getenv("NEXUS_PASSWORD") ?: findProperty("nexusPassword")
            }
        }
    }
}

jar {
    manifest {
        attributes(
            "Implementation-Title": project.name,
            "Implementation-Version": project.version
        )
    }
}

repositories {
    mavenCentral()
}
sourceSets {
    main.java.srcDirs = ['src/main/java']
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task execute(type:JavaExec) {
   main = System.getProperty('mainClass')
   classpath = sourceSets.main.runtimeClasspath
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

dependencies {
    implementation 'io.swagger:swagger-annotations:1.6.16'
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'io.gsonfire:gson-fire:1.9.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'org.threeten:threetenbp:1.7.2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.24.3'
    testImplementation 'junit:junit:4.13.2'
}

javadoc {
    options.tags = [ "http.response.details:a:Http Response Details" ]
}
